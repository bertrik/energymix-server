plugins {
    alias libs.plugins.spotbugs
    alias libs.plugins.versions
    alias libs.plugins.dependencycheck
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyCheck {
    scanConfigurations = ['runtimeClasspath']
}

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "com.github.spotbugs"
    apply plugin: 'com.github.ben-manes.versions'
    
    targetCompatibility = 1.14
    sourceCompatibility = 1.14

    repositories {
        mavenCentral()
    }

    // Reject all non stable versions in dependency check
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }

    // Standard libraries added to all projects
    dependencies {
        testImplementation libs.junit
        compileOnly libs.spotbugs.annotations
    }
}

